buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version("1.8.4")
    id 'java'
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'org.jlleitschuh.gradle.ktlint' version '9.2.1'
    id 'jacoco'
    id 'maven-publish'
}


sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

group 'dali.hamza'
version buildVersion(rootProject)

static def buildVersion(rootProject) {
    def gradleV = rootProject.getProperties()["version"]
    if (rootProject.hasProperty("dev-version")) {
            return rootProject.getProperties()["dev-version"]
    }
    return gradleV
}

sourceCompatibility = 1.8
configure([compileKotlin, compileTestKotlin]) {
    kotlinOptions {
        jvmTarget = "$sourceCompatibility"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.21"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'io.github.serpro69:kotlin-faker:1.4.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    // Adjust the output of the test report
    reports {
        xml.enabled true
        csv.enabled true
    }
}
task sourcesJar(type: Jar) {
    classifier('source')
    from sourceSets.main.allSource
}
artifacts {
    archives sourcesJar
}
def props = new Properties()
def file = file("github.properties")
if (file.exists())
    file.withInputStream { props.load(it) }


// Attempts to read the GPR user from local
// project properties and then system environment variables.
static def getGprUser(props) {
    if (props.getProperty("gpr.user") != null) {
        return props.getProperty("gpr.user")
    } else {
        return System.getenv("GPR_USER") ?: ""
    }
}
// Attempts to read the GPR key from local
// project properties and then system environment variables.
static def getGprKey(props) {
    if (props.getProperty("gpr.key") != null) {
        return props.getProperty("gpr.key")
    } else {
        return System.getenv("GPR_KEY") ?: ""
    }
}


def user = getGprUser(props)
def key = getGprKey(props)

publishing {

    repositories {
        maven {
            name = "Mapster-Kotlin"
            url = uri("https://maven.pkg.github.com/$user/KotlinMapster")
            credentials {
                username = user
                password = key
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.hamza.dali'
            artifactId = "mapster-ktx"
            version = "$version"
            from components.java
            pom {
                name = 'MapsterKotlin'
                description = 'Fun and easy mapper library.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

apply from: 'bintray.gradle'